using elsystem; 
using elsystem.windows.forms; 
using elsystem.drawing; 
 
inputs: 
	Sym("MSFT"), 
	int MinuteInterval(5), 
	LastDate("09/30/2013"), 
	int DaysBack(90), 
	NumDevsStep(0.1); 
	 
variables: 
	Form form1(null), 
	Button StartButton(null), 
	ProgressBar PBar(null), 
	tsopt.Optimizer optimizer(null); 
 
method void OnStartButtonClick(elsystem.Object sender, elsystem.EventArgs args) 
begin 
	StartOptimization(); 
end; 
 
method tsopt.Job DefineJob() 
vars: 
	tsopt.Job job, 
	tsopt.Security security, 
	tsopt.Strategy strategy; 
begin 
	job = new tsopt.Job; 
	security = job.Securities.AddSecurity(); 
	security.Symbol = Sym; 
	security.Interval.SetMinuteChart(MinuteInterval); 
	security.History.LastDateString = LastDate; 
	security.History.DaysBack = DaysBack; 
	strategy = job.Strategies.AddStrategy("Bollinger Bands LE"); 
	strategy.ELInputs.OptRange("NumDevsDn", 1, 3, NumDevsStep); 
	strategy = job.Strategies.AddStrategy("Bollinger Bands SE"); 
	strategy.ELInputs.OptRange("NumDevsUp", 1, 3, NumDevsStep); 
	return job; 
end; 
 
method void StartOptimization() 
vars: 
	tsopt.Job job; 
begin 
ClearPrintLog; 
	Print("Starting optimization..."); 
	job = DefineJob(); 
	optimizer = new tsopt.Optimizer; 
	optimizer.JobDone += OptDone; 
	optimizer.JobFailed += OptError; 
	optimizer.ProgressChanged += OptProgress; 
	PBar.Value = 0; // Reset progress bar value to 0% 
	optimizer.StartJob(job); 
end; 
 
method void OptProgress(Object sender, tsopt.ProgressChangedEventArgs args) 
vars: 
	int pct; 
begin 
 
	// Calculate current progress bar percentage 
	if args.Progress.TestCount <> 0 then 
		pct = Round((args.Progress.TestNum / args.Progress.TestCount) * 100, 0) astype int;	 
	PBar.Value = pct; 
	 
	Print("Test ", args.Progress.TestNum.ToString(), " of ", 
	 args.Progress.TestCount.ToString()); 
	Print(" ", args.BestValues.FitnessName, " = ", 
	 args.BestValues.FitnessValue.ToString()); 
	  
end; 
 
method void OptDone(object sender, tsopt.JobDoneEventArgs args) 
begin 
	PBar.Value = 100; // Set progress bar to 100% 
	Print("Optimization done"); 
	Print("Net Profit = ", args.Results.NetProfit()); 
end; 
 
method void OptError(Object sender, tsopt.JobFailedEventArgs args) 
begin 
	PBar.Value = 100; // Set progress bar to 100% 
	Print("Optimization Error: ", args.Error.Message); 
end; 
 
once 
begin 
 
	form1 = Form.Create("OptTest", 500, 500); 
	form1.BackColor = Color.LightGray; 
	form1.Dock = DockStyle.Right; 
	startButton = Button.Create("Start Optimization", 120, 30); 
	startButton.Location(20, 20); 
	startButton.Click += OnStartButtonClick; 
	form1.AddControl(StartButton); 
	 
	// Create progress bar and add to form 
	PBar = ProgressBar.Create(120, 24); 
	PBar.Location(20, 60); 
	form1.AddControl(PBar); 
	 
	form1.Show(); 
	 
end;
